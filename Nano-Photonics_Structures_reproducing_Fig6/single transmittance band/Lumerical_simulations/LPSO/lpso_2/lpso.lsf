#PSO
gen=200;             #The number of iterations.
sizepop=50;         #The number of particles in the swarm.
c1=1.4962;         
c2=1.4962;
w=0.7968;
tar=readdata('data_single_peak.txt'); 
popcen=[80,80,80,80,80];
popmin=[15,15,15,15,15];         
popmax=[150,150,150,150,150]; 
M=5;                #Dimension  
Vmax=0.2*(150-15);   
Vmin=-1*0.2*(150-15);
result=matrix(1,gen);
V=matrix(sizepop,M);
trans_result=matrix(sizepop,201);
pop=matrix(sizepop,M);
for(ii=1:sizepop){
    pop(ii,:)=popcen+round(70*(1-2*randmatrix(1,M)));
    V(ii,:)=Vmax*(1-2*randmatrix(1,M));
}
fitness=matrix(1,sizepop);
for(ii=1:sizepop){
    ?ii;
    del('d.txt');
    d=[pop(ii,1:M),pop(ii,M-1:-1:1)];
    d=[d,d];    
    write('d.txt',num2str(d));
    switchtolayout;    
    select('structure group');    
    runsetup;
    select('mesh');
    set('x min',-(sum(d)*1e-9));
    run;
    temp=getresult('transmission','T');
    trans_result(ii,:)=-pinch(temp.T);
    fitness(1,ii)=sum(abs(tar-trans_result(ii,1:201)),2);
}
matlabput(fitness);
matlab("
    [bestf,besti]=min(fitness);
");
matlabget(bestf,besti);
pbest=pop;
gbest=pop(besti,:);
fitnesspbest=fitness;
fitnessgbest=bestf;

for(ii=1:gen){
    for(jj=1:sizepop){
        ?ii;
        ?jj;
        if (jj==1)
        {
            if(fitnesspbest(sizepop)<fitnesspbest(2)){lbest=pbest(sizepop,:);}
            else {lbest=pbest(2,:);}
        }
        else if (jj==sizepop)
        {
            if(fitnesspbest(sizepop-1)<fitnesspbest(1)){lbest=pbest(sizepop-1,:);}
            else {lbest=pbest(1,:);}
        }
        else 
        {
            if(fitnesspbest(jj-1)<fitnesspbest(jj+1)){lbest=pbest(jj-1,:);}
            else {lbest=pbest(jj+1,:);}
        }
        V(jj,:)=w*V(jj,:)+c1*randmatrix(1,M)*(pbest(jj,:)-pop(jj,:))+c2*randmatrix(1,M)*(lbest-pop(jj,:));        
        for(kk=1:M){        
            if(V(jj,kk)>Vmax){
                V(jj,kk)=Vmax;
            }
            if(V(jj,kk)<Vmin){
                V(jj,kk)=Vmin;
            }
        }
        pop(jj,:)=round(pop(jj,:)+V(jj,:));
        for(kk=1:M){
            if(pop(jj,kk)>popmax(kk)){
                pop(jj,kk)=popmax(kk);
            }
            if(pop(jj,kk)<popmin(kk)){
                pop(jj,kk)=popmin(kk);
            }
        }
        del('d.txt');
        d=[pop(jj,1:M),pop(jj,M-1:-1:1)];   
        d=[d,d]; 
        write('d.txt',num2str(d));
        switchtolayout;    
        select('structure group');    
        runsetup;
        select('mesh');
        set('x min',-(sum(d)*1e-9));
        run;
        temp=getresult('transmission','T');
        trans_result(jj,:)=-pinch(temp.T);
        fitness(1,jj)=sum(abs(tar-trans_result(jj,1:201)),2);
        if(fitness(jj)<fitnesspbest(jj)){
            pbest(jj,:)=pop(jj,:);
            fitnesspbest(jj)=fitness(jj);
        }
        if(fitness(jj)<fitnessgbest){
            gbest=pop(jj,:);
            fitnessgbest=fitness(jj);
        }
    }
    result(ii)=fitnessgbest;
    matlabsave('pso-result'+num2str(ii),pbest,gbest,V,fitnessgbest,ii,fitnesspbest,trans_result,pop,result);
}
matlabsave('result',result,gbest);
    
